//@version=5
indicator("Market Structure HH, HL, LH and LL", overlay=true, max_lines_count=500, max_bars_back=4900, max_boxes_count=500)

settings = "Settings"
zigzag_len = input.int(40, "ZigZag Length", group=settings)
show_zigzag = input.bool(true, "Show Zigzag", group=settings)
zigzag_color = input.color(color.blue, "ZigZag Line Color", group=settings)
zigzag_width = input.int(2, "ZigZag Line Width", group=settings, minval=1, maxval=5)
show_labels = input.bool(true, "Show HH, HL, LL, LH Labels", group=settings)
fib_factor = input.float(0.33, "Fib Factor for breakout confirmation", 0, 1, 0.01, group=settings)

text_size = input.string(size.tiny, "Text Size", [size.tiny, size.small, size.normal, size.large, size.huge], group=settings)

var float[] high_points_arr = array.new_float(5)
var int[] high_index_arr = array.new_int(5)
var float[] low_points_arr = array.new_float(5)
var int[] low_index_arr = array.new_int(5)

to_up = high >= ta.highest(zigzag_len)
to_down = low <= ta.lowest(zigzag_len)

trend = 1
trend := nz(trend[1], 1)
trend := trend == 1 and to_down ? -1 : trend == -1 and to_up ? 1 : trend

last_trend_up_since = ta.barssince(to_up[1])
low_val = ta.lowest(nz(last_trend_up_since > 0 ? last_trend_up_since : 1))
low_index = bar_index - ta.barssince(low_val == low)

last_trend_down_since = ta.barssince(to_down[1])
high_val = ta.highest(nz(last_trend_down_since > 0 ? last_trend_down_since : 1))
high_index = bar_index - ta.barssince(high_val == high)

if ta.change(trend) != 0
    if trend == 1
        array.push(low_points_arr, low_val)
        array.push(low_index_arr, low_index)
    if trend == -1
        array.push(high_points_arr, high_val)
        array.push(high_index_arr, high_index)

h0 = array.size(high_points_arr) > 1 ? array.get(high_points_arr, array.size(high_points_arr) - 1) : na
h0i = array.size(high_index_arr) > 1 ? array.get(high_index_arr, array.size(high_index_arr) - 1) : na
h1 = array.size(high_points_arr) > 2 ? array.get(high_points_arr, array.size(high_points_arr) - 2) : na
h1i = array.size(high_index_arr) > 2 ? array.get(high_index_arr, array.size(high_index_arr) - 2) : na

l0 = array.size(low_points_arr) > 1 ? array.get(low_points_arr, array.size(low_points_arr) - 1) : na
l0i = array.size(low_index_arr) > 1 ? array.get(low_index_arr, array.size(low_index_arr) - 1) : na
l1 = array.size(low_points_arr) > 2 ? array.get(low_points_arr, array.size(low_points_arr) - 2) : na
l1i = array.size(low_index_arr) > 2 ? array.get(low_index_arr, array.size(low_index_arr) - 2) : na

if ta.change(trend) != 0
    if show_zigzag
        if trend == 1
            line.new(h0i, h0, l0i, l0, color=zigzag_color, width=zigzag_width, style=line.style_solid, extend=extend.none)
        if trend == -1
            line.new(l0i, l0, h0i, h0, color=zigzag_color, width=zigzag_width, style=line.style_solid, extend=extend.none)
    
    // Labeling HH, HL, LL, LH
    if show_labels
        if h0 > h1
            label.new(h0i, h0, "HH", color=color.green, textcolor=color.white, size=text_size, style=label.style_label_down)
        else
            label.new(h0i, h0, "LH", color=color.red, textcolor=color.white, size=text_size, style=label.style_label_down)
        
        if l0 < l1
            label.new(l0i, l0, "LL", color=color.red, textcolor=color.white, size=text_size, style=label.style_label_up)
        else
            label.new(l0i, l0, "HL", color=color.green, textcolor=color.white, size=text_size, style=label.style_label_up)















